/* MACRO definitions for saving and restoring task state. */

.extern cpu_get_currcpu_info

.macro TASK_SAVE_IRQ_CONTEXT
    STP 	X0, X1, [SP, #-0x10]!
	STP 	X2, X3, [SP, #-0x10]!
	STP 	X4, X5, [SP, #-0x10]!
	STP 	X6, X7, [SP, #-0x10]!
	STP 	X8, X9, [SP, #-0x10]!
	STP 	X10, X11, [SP, #-0x10]!
	STP 	X12, X13, [SP, #-0x10]!
	STP 	X14, X15, [SP, #-0x10]!
	STP 	X16, X17, [SP, #-0x10]!
	STP 	X18, X19, [SP, #-0x10]!
	STP 	X20, X21, [SP, #-0x10]!
	STP 	X22, X23, [SP, #-0x10]!
	STP 	X24, X25, [SP, #-0x10]!
	STP 	X26, X27, [SP, #-0x10]!
	STP 	X28, X29, [SP, #-0x10]!
	STP 	X30, XZR, [SP, #-0x10]!

    MRS		X3, SPSR_EL1
	MRS		X2, ELR_EL1
	STP 	X2, X3, [SP, #-0x10]!

    bl cpu_get_currcpu_info

    ldr x1, [x0]
    /* Store the current stack pointer into the current task stack top */
    mov x2, sp
    str x2, [x1]
.endm


.macro TASK_SAVE_SYNC_CONTEXT
    STP 	X0, X1, [SP, #-0x10]!
	STP 	X2, X3, [SP, #-0x10]!
	STP 	X4, X5, [SP, #-0x10]!
	STP 	X6, X7, [SP, #-0x10]!
	STP 	X8, X9, [SP, #-0x10]!
	STP 	X10, X11, [SP, #-0x10]!
	STP 	X12, X13, [SP, #-0x10]!
	STP 	X14, X15, [SP, #-0x10]!
	STP 	X16, X17, [SP, #-0x10]!
	STP 	X18, X19, [SP, #-0x10]!
	STP 	X20, X21, [SP, #-0x10]!
	STP 	X22, X23, [SP, #-0x10]!
	STP 	X24, X25, [SP, #-0x10]!
	STP 	X26, X27, [SP, #-0x10]!
	STP 	X28, X29, [SP, #-0x10]!
	STP 	X30, XZR, [SP, #-0x10]!

    MRS		X3, SPSR_EL1
	/* Store the link register as we are making a syncronous call back to a function */
	mov 	X2, x30
	STP 	X2, X3, [SP, #-0x10]!

    bl cpu_get_currcpu_info

	ldr x1, [x0] /* x1 = currcpu->(task_t *)curr_task */
    /* Store the current stack pointer into the current task stack top */
    mov x2, sp
    str x2, [x1] /* *(task_t *)curr_task = sp */
.endm

.macro TASK_RESTORE_CONTEXT
    bl cpu_get_currcpu_info
    ldr x1, [x0] /* x1 = currcpu->(task_t *)curr_task  */
    ldr x2, [x1] /* (uint64_t/el1_stack_ptr) x2 = *(task_t)curr_task */
    mov sp, x2

    ldp x2, x3, [sp], #0x10 

    /* Restore spsr, elr */
    msr spsr_el1, x3
    msr elr_el1, x2

    LDP 	X30, XZR, [SP], #0x10
	LDP 	X28, X29, [SP], #0x10
	LDP 	X26, X27, [SP], #0x10
	LDP 	X24, X25, [SP], #0x10
	LDP 	X22, X23, [SP], #0x10
	LDP 	X20, X21, [SP], #0x10
	LDP 	X18, X19, [SP], #0x10
	LDP 	X16, X17, [SP], #0x10
	LDP 	X14, X15, [SP], #0x10
	LDP 	X12, X13, [SP], #0x10
	LDP 	X10, X11, [SP], #0x10
	LDP 	X8, X9, [SP], #0x10
	LDP 	X6, X7, [SP], #0x10
	LDP 	X4, X5, [SP], #0x10
	LDP 	X2, X3, [SP], #0x10
	LDP 	X0, X1, [SP], #0x10

    /* return to the addresses that was saved in the elr */
    eret
.endm
